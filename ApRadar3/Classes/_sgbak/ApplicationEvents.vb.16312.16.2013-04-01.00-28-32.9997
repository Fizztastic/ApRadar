
Namespace My

    ' The following events are available for MyApplication:
    ' 
    ' Startup: Raised when the application starts, before the startup form is created.
    ' Shutdown: Raised after all application forms are closed.  This event is not raised if the application terminates abnormally.
    ' UnhandledException: Raised if the application encounters an unhandled exception.
    ' StartupNextInstance: Raised when launching a single-instance application and the application is already active. 
    ' NetworkAvailabilityChanged: Raised when the network connection is connected or disconnected.
    Partial Friend Class MyApplication
        Public Delegate Sub DisposeDelegate()

        Private Sub MyApplication_Shutdown(ByVal sender As Object, ByVal e As System.EventArgs) Handles Me.Shutdown
            If NeedsUpdate Then
                Process.Start("http://services.apradar.com/files/ApRadar3Update.exe")
            End If
        End Sub

        Private Sub MyApplication_Startup(ByVal sender As Object, ByVal e As Microsoft.VisualBasic.ApplicationServices.StartupEventArgs) Handles Me.Startup
            If My.Settings.UpgradeRequired Then
                My.Settings.Upgrade()
                My.Settings.UpgradeRequired = False
                My.Settings.Save()
            End If

            'Load the current theme
            CType(SplashScreen, ARSplash).Message = "Loading theme..."
            ThemeHandler.LoadTheme(My.Settings.CurrentTheme)

            CType(SplashScreen, ARSplash).Message = "Validating version..."
            Dim minVersion As String = ""
            Dim valid = ValidateVersion(minVersion)
            If (valid <> VerificationResult.Valid) Then
                'If we are downloading a new version then cancel loading
                e.Cancel = True
                If My.Application.SplashScreen IsNot Nothing Then
                    Dim splashScreenDispose As New My.MyApplication.DisposeDelegate(AddressOf My.Application.SplashScreen.Dispose)
                    My.Application.SplashScreen.Invoke(splashScreenDispose)
                    My.Application.SplashScreen = Nothing
                End If
                If valid = VerificationResult.NoConnection Then
                    MessageBox.Show("ApRadar is unable to contact the ApRadar servers, please check your internet connection and try again", "Unable to validate version", MessageBoxButtons.OK)
                Else
                    If MessageBox.Show("Your version is out of date, please update to the minimum version (" & _
                                   minVersion & ").  To update now, please click OK, otherwise click Cancel to quit.",
                                   "Version out of date", MessageBoxButtons.OKCancel) = DialogResult.OK Then
                        UpdateApplication(minVersion)
                    End If
                End If
                
                Exit Sub
            End If

            CType(SplashScreen, ARSplash).Message = "Checking for updates..."
            'First thing, lets check to see if there is an updated version
            If My.Settings.CheckAppUpdates Then
                'Check for a new version and notify the user if one exists
                If CheckApplicationVersion() Then
                    'If we are downloading a new version then cancel loading
                    e.Cancel = True
                    

                    Exit Sub
                End If
            End If

            CType(SplashScreen, ARSplash).Message = "Checking for map updates..."
            'If they want to check at startup, lets see if there are any new maps
            If My.Settings.CheckMapUpdates Then
                CheckMapVersion()
            End If

            CType(SplashScreen, ARSplash).Message = "Validating pro..."
            Try
                'Check for ApRadar Pro or EliteMMO Subscribers
                VerifyPro()
            Catch ex As Exception
                MessageBox.Show(ex.Message)
                GlobalSettings.IsProEnabled = False
            End Try

            CType(SplashScreen, ARSplash).Message = "Launching ApRadar..."
            'Last thing, lets check if we should start in nobar mode
            If e.CommandLine.Count > 0 Then
                For Each arg As String In e.CommandLine
                    If arg = "/nobar" Then
                        NoBar = True
                        Application.IsSingleInstance = False
                    ElseIf arg = "/d" OrElse arg = "/debug" Then
                        DebugRun = True
                    End If
                Next
            End If
        End Sub

        Private Sub MyApplication_UnhandledException(ByVal sender As Object, ByVal e As Microsoft.VisualBasic.ApplicationServices.UnhandledExceptionEventArgs) Handles Me.UnhandledException
            Using ued As New UnhandledErrorDialog(e.Exception)
                ued.ShowDialog()
            End Using
        End Sub
    End Class
End Namespace

